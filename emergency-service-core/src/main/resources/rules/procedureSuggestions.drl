package phoneCallPrimaryService.procedureSuggestion;


import org.drools.runtime.process.WorkflowProcessInstance;
import java.util.Map;
import java.util.HashMap;
import com.wordpress.salaboy.model.*;
import com.wordpress.salaboy.model.Emergency.EmergencyType;
import com.wordpress.salaboy.model.messages.EmergencyDetailsMessage;
import com.wordpress.salaboy.model.serviceclient.*;
import com.wordpress.salaboy.model.serviceclient.PersistenceServiceProvider.*;
import com.wordpress.salaboy.messaging.MessageFactory;

rule "Initialize Emergency"
    salience 20
    ruleflow-group "suggest process"
    when
        
        $processInstance: WorkflowProcessInstance( )
        not(Emergency(processInstanceId == $processInstance.id))
    then
        Emergency emergency = (Emergency)$processInstance.getVariable("emergency");
        
        String callId = ((Call)$processInstance.getVariable("call")).getId();
        System.out.println(">>>>>>> Emergency.call.id == "+((Emergency)$processInstance.getVariable("emergency")).getCall().getId() +" - call.id =="+callId);
        
        ActivePatients activePatients = new ActivePatients(emergency.getId());
        for(int i = 0; i < emergency.getNroOfPeople(); i ++){
            //I should call the tracking component to attach a new patient and get the ID
            Patient patient = new Patient();
            patient.setAge(25); //just to activete the next rule.. we should enter this information in a map -> patientsDetails??
            activePatients.addPatient(patient);
            insert(patient);
        }
        $processInstance.setVariable("activePatients",activePatients);
        emergency.setProcessInstanceId($processInstance.getId());
        $processInstance.setVariable("emergency.id",emergency.getId());
        //Notify about the new Emergency
        MessageFactory.sendMessage(new EmergencyDetailsMessage(emergency));
        insert(emergency);
        insert(new SuggestedProcedures(emergency.getId()));
end


rule "Suggest Heart Attack Default Procedure for 1 Patient > 20 && < 60"
    ruleflow-group "suggest process"
    when
        $processInstance: WorkflowProcessInstance()
        $emergency: Emergency(nroOfPeople == 1, processInstanceId == $processInstance.id)
        $patient: Patient(age > 20 && < 60)
        $procedures: SuggestedProcedures(emergencyId == $emergency.id)
    then
       System.out.println(">>>>>> Adding a Suggested Procedure: "+"DefaultHeartAttackProcedure");
       $procedures.addProcedureName("DefaultHeartAttackProcedure"); 
       $processInstance.setVariable("suggestedProcedures",$procedures);
end

rule "Suggest Default Fire Procedure for Fire Emergency"
    ruleflow-group "suggest process"
    when
        $processInstance: WorkflowProcessInstance()
        $emergency: Emergency(type == Emergency.EmergencyType.FIRE, processInstanceId == $processInstance.id)
        $procedures: SuggestedProcedures(emergencyId == $emergency.id)
    then
       System.out.println(">>>>>> Adding a Suggested Procedure: "+"DefaultFireProcedure");
       $procedures.addProcedureName("DefaultFireProcedure"); 
       $processInstance.setVariable("suggestedProcedures",$procedures);
end

